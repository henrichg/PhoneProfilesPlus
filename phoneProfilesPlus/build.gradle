apply plugin: 'com.android.application'
apply plugin: 'io.fabric'
apply from: '../dependencies.gradle'

android {
    compileSdkVersion ppVersions.compileSdk
    buildToolsVersion ppVersions.buildTools

    //noinspection GroovyMissingReturnStatement
    defaultConfig {
        applicationId "sk.henrichg.phoneprofilesplus"
        minSdkVersion ppVersions.minSdk
        targetSdkVersion ppVersions.targetSdk
        versionCode ppVersions.publishVersionCode
        versionName ppVersions.publishVersion
        multiDexEnabled true

        compileOptions {
            sourceCompatibility JavaVersion.VERSION_1_7
            targetCompatibility JavaVersion.VERSION_1_7
        }

        resConfigs "en", "ar", "bg", "ca", "cs", "de", "el", "es", "fa", "fr", "hi", "hu", "in", "it",
                "iw", "ja", "ko", "nl", "pl", "pt-rBR", "pt-rPT", "ro", "ru", "sk", "sl", "sv", "te",
                "tr", "uk", "zh-rCN", "zh-rTW"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        debug {
            debuggable true
            // Disable fabric build ID generation for debug builds
            ext.enableCrashlytics = false
        }
    }

    //noinspection GroovyMissingReturnStatement
    lintOptions {
        //checkReleaseBuilds false
        // Or, if you prefer, you can continue to check for errors in release builds,
        // but continue the build even when errors are found:
        //abortOnError false
        disable 'MissingTranslation'
        disable 'ManifestResource'
        disable 'LongLogTag'
    }
}

dependencies {
    implementation 'com.android.support:multidex:1.0.2'
    implementation 'com.android.support:support-core-ui:' + ppVersions.supportLib
    implementation 'com.android.support:appcompat-v7:' + ppVersions.supportLib
    implementation 'com.android.support:gridlayout-v7:' + ppVersions.supportLib
    implementation 'com.android.support:cardview-v7:' + ppVersions.supportLib
    implementation 'com.android.support:recyclerview-v7:' + ppVersions.supportLib
    implementation 'com.android.support:design:' + ppVersions.supportLib
    implementation 'com.google.android.gms:play-services-location:11.6.2'
    implementation 'com.google.android.gms:play-services-maps:11.6.2'

    implementation 'com.google.code.gson:gson:2.8.0'
    implementation('com.crashlytics.sdk.android:crashlytics:2.7.1@aar') {
        transitive = true
    }

    implementation 'com.evernote:android-job:1.2.1'
    implementation 'com.google.android.apps.dashclock:dashclock-api:2.0.0'
    implementation 'com.readystatesoftware.systembartint:systembartint:1.0.4'
    implementation 'com.afollestad.material-dialogs:core:0.9.6.0'
    implementation 'com.labo.kaji:relativepopupwindow:0.3.1'
    implementation 'com.github.KeepSafe:TapTargetView:master-SNAPSHOT'
    implementation 'com.code-troopers.betterpickers:library:3.1.0'
    implementation 'mobi.upod:time-duration-picker:1.1.3'

    implementation project(':QuickScroll')

    implementation(name:'RootShell', ext:'aar')
    implementation(name:'RootTools', ext:'aar')
    compileOnly files('libs/hidden-apis.jar')
    compileOnly files('libs/hidden-apis_2.jar')
    implementation files('libs/sdk-v1.0.0.jar')
    implementation files('libs/slook_v1.4.0.jar')

}


