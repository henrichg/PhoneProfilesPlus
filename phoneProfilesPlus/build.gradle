apply plugin: 'com.android.application'
apply from: '../dependencies.gradle'
apply from: '../passwords_keys.gradle'

android {
    // This is IMORTANT dfor combinig of release and debug versions of PPP, PPPE, PPPPS.
    // Also in PPPE a PPPPS is this and must be the same!
    signingConfigs {
        debug {
            storeFile file(passwordsKeys.appplication_storeFile)
            storePassword passwordsKeys.storePassword
            keyAlias passwordsKeys.keyAlias
            keyPassword passwordsKeys.keyPassword
        }
    }
    namespace 'sk.henrichg.phoneprofilesplus'

    compileSdkVersion ppVersions.compileSdk
    buildToolsVersion ppVersions.buildTools

    defaultConfig {
        applicationId "sk.henrichg.phoneprofilesplus"
        minSdkVersion ppVersions.minSdk
        targetSdkVersion ppVersions.targetSdk
        versionCode ppVersions.publishVersionCode
        versionName ppVersions.publishVersion
        //multiDexEnabled true
        vectorDrawables.useSupportLibrary = true
        resourceConfigurations += ['en', 'af', 'ar', 'bn-rIN', 'bg', 'ca', 'cs', 'da', 'de', 'el', 'es', 'et', 'fa', 'fi', 'fr', 'ga-rIN', 'hi', 'hr', 'hu', 'in', 'is', 'it', 'iw', 'ja', 'ko', 'ku', 'lb', 'lv', 'ms', 'nl', 'no', 'pl', 'pt-rBR', 'pt-rPT', 'ro', 'ru', 'sk', 'sl', 'sr', 'b+sr+Latn', 'sv', 'te', 'th', 'tr', 'uk', 'vi', 'zh-rCN', 'zh-rTW']
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }

    buildTypes {
        release {
            minifyEnabled false
            buildConfigField "String", "encrypt_contacts_key", "\"$passwordsKeys.encrypt_contacts_key\""
            buildConfigField "String", "encrypt_contacts_salt", "\"$passwordsKeys.encrypt_contacts_salt\""
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            /*firebaseCrashlytics {
                mappingFileUploadEnabled true
            }*/
        }
        debug {
            minifyEnabled false
            debuggable true
            buildConfigField "String", "encrypt_contacts_key", "\"$passwordsKeys.encrypt_contacts_key\""
            buildConfigField "String", "encrypt_contacts_salt", "\"$passwordsKeys.encrypt_contacts_salt\""
            /*firebaseCrashlytics {
                mappingFileUploadEnabled true
            }*/
        }
    }
    lint {
        abortOnError false
        disable += ['ManifestResource', 'LongLogTag', 'AppBundleLocaleChanges']
        disable += ['ExpiredTargetSdkVersion']
    }
    bundle {
        language {
            // this keeps all language strings
            enableSplit = false
        }
    }

    // Requested by IzzyOnDroid. PPP is not in Google Play. For this reason may be both false.
    // https://developer.android.com/reference/tools/gradle-api/8.2/com/android/build/api/dsl/DependenciesInfo
    dependenciesInfo {
        // Disables dependency metadata when building APKs.
        includeInApk = false
        // Disables dependency metadata when building Android App Bundles.
        includeInBundle = false
    }
    buildFeatures {
        buildConfig true
    }

}

dependencies {
    /// WARNING:
    // Must be used android.jar downloaded from https://github.com/Reginer/aosp-android-jar
    // Downloaded android.jar must be copied into PC into folder \<android-sdk\>/platforms/android-XX

    //debugImplementation 'com.squareup.leakcanary:leakcanary-android:2.12'

    //implementation 'androidx.multidex:multidex:2.0.1'

    /// for usage of @hidden Android API
    // https://mvnrepository.com/artifact/org.lsposed.hiddenapibypass/hiddenapibypass
    implementation 'org.lsposed.hiddenapibypass:hiddenapibypass:6.1'//6.0'
    //implementation 'org.lsposed.hiddenapibypass:hiddenapibypass:+' // not good, generates warning

    /// ACRA
    implementation 'com.google.guava:guava:33.4.8-android' //33.4.0-android'
    implementation "ch.acra:acra-mail:$ppVersions.acra"
    implementation "ch.acra:acra-notification:$ppVersions.acra"
    // https://github.com/ACRA/acra/tree/master/examples/acra-basic-java-example
    annotationProcessor("com.google.auto.service:auto-service:$ppVersions.autoservice")
    compileOnly("com.google.auto.service:auto-service-annotations:$ppVersions.autoservice")
    /////

    /// for usage of WorkManager
    implementation 'androidx.work:work-runtime:2.10.1' //2.9.1' //2.8.1'

    /// for fix bitmaps orietnation
    implementation 'androidx.exifinterface:exifinterface:1.4.1' //1.3.7

    //implementation "androidx.core:core-splashscreen:1.0.1"

    implementation "androidx.recyclerview:recyclerview:$ppVersions.recyclerView"
    implementation 'androidx.gridlayout:gridlayout:1.1.0'
    implementation 'androidx.cardview:cardview:1.0.0'
    implementation 'androidx.viewpager2:viewpager2:1.1.0'

    // for get dominant color from profile icons
    // - for notification decorator
    // - for increase brightness of profile icon color - for dark theme of GUI
    implementation 'androidx.palette:palette:1.0.0'

    // 1.1.1 is required because of not working title style (Spannable) in 1.2.0
    // When changed is only style, but not text, style is not aplied. :-(
    //noinspection GradleDependency
    implementation 'androidx.preference:preference:1.1.1'

    //implementation 'androidx.activity:activity:1.9.1'
    implementation "androidx.appcompat:appcompat:$ppVersions.appcompatXLib"
    implementation "androidx.appcompat:appcompat-resources:$ppVersions.appcompatXLib"

    /*
    // appcompat 1.7.0 - dependencies problem, this fixed it
    constraints {
        //noinspection ForeignDelegate
        implementation("org.jetbrains.kotlin:kotlin-stdlib-jdk8:$ppVersions.kotlinStdlibJdk8")
    }
    */

    // 1.11.0' is required for PPP, because is needed for Profile indicator with dynamic color
    //noinspection GradleDependency
    implementation 'com.google.android.material:material:1.10.0'

    /// for bottom bar in Editor
    implementation 'com.github.ibrahimsn98:SmoothBottomBar:1.7.9'

    /// for map display in Location editor dialog
    implementation 'org.osmdroid:osmdroid-android:6.1.20' //6.1.18'

    /// for json usage in some preferenses (wifi, bluetooth connection, scanning, ...)
    implementation 'com.google.code.gson:gson:2.13.1'//2.12.1'

    /// for DashClock support
    implementation 'com.google.android.apps.dashclock:dashclock-api:2.0.0'

    // for GUI click help (Editor menu / Help with GUI items)
    implementation 'com.getkeepsafe.taptargetview:taptargetview:1.15.0'//1.14.0' //'1.13.3'

    /// for DontKillMyApp dialog
    // required for dev.doubledot.doki.views.DokiContentView -
    implementation 'androidx.constraintlayout:constraintlayout:2.2.1' //2.2.0'
    implementation 'dev.doubledot.doki:library:0.0.1'
    //implementation('dev.doubledot.doki:library:0.0.1@aar') {
    //    transitive = true
    //}
    /////

    /// for read PPP, PPPE, PPPPS latest release from IzzyOnDroid repository
    implementation 'com.android.volley:volley:1.2.1'

    /// for ExpandableLayout used in Important info
    implementation "com.github.skydoves:expandablelayout:1.0.7"

    /// for Wi-Fi AP profile parameter
    implementation 'com.linkedin.dexmaker:dexmaker:2.28.4'

    /// for backup/restore encripted data
    implementation 'com.github.simbiose:Encryption:2.0.1'

    /// for Shizuku usage of profile parameters in non-rooted devices
    //noinspection GradleDependency
    implementation "dev.rikka.shizuku:api:$ppVersions.shizuku"
    //noinspection GradleDependency
    implementation "dev.rikka.shizuku:provider:$ppVersions.shizuku"
    /////


    implementation project(':common')
    implementation project(':ToastCompat')
    implementation project(':RootShell')
    implementation project(':RootTools')
    implementation project(':android-betterpickers')
    implementation project(':time-duration-picker')
    implementation project(':AndroidClearChroma')
    implementation project(':recyclerview-fastscroll')
    implementation project(':noobcameraflash')
}
